plugins {
    id 'java-library'
    id 'eclipse'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.0.0-rc-3'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8 

apply from: '../dependencies.gradle'
apply from: 'integration-test.gradle'

configurations.all {
    resolutionStrategy { 
        force 'ch.interlis:iox-ili:1.20.15'
        force 'ch.interlis:iox-wkf:1.0.2'
        force 'ch.interlis:ili2c-tool:5.0.2'
        force 'ch.interlis:ili2c-core:5.0.2'
        force 'ch.interlis:ili2pg:4.3.2'
        force 'ch.interlis:ili2gpkg:4.3.2'
    }
}

/*
 * This is ridiculous.
 */
// TODO: Ist das noch notwendig, da jetzt die Treiber
// in maven central frei verfügbar sind? Solange
// es nicht schadet, ist es ja ok, wenn wir möglichst
// wenig 'Müll' mitschleppen.
configurations {
    integrationTestImplementation {
        exclude module: 'xmlparserv2'
        exclude module: 'ucp'
        exclude module: 'xdb6'
        exclude module: 'oraclepki'
        exclude module: 'osdt_core'
        exclude module: 'osdt_cert'
        exclude module: 'orai18n' 
    }
    
    all*.exclude module: 'spring-boot-starter-logging'
    
}

dependencies {
    api gradleApi()
  
    api ilivalidatorDependency
    api ilivalidatorCustomFunctionsDependency
    api (ili2pgDependency) {
        exclude group: 'ch.ehi', module: 'ehisqlgen'
    }
    api ioxWkfDependency
    api ioxIliDependency
    api ehiSqlgenDependency

    api oerebIconizerDependency
    api av2chDependency
    api av2geobauDependency
    
    //api awsSdkDependency
    api awsSdkS3Dependency
    
    api apacheCommonsIoDependency
    api apacheCommonsNetDependency
    api apacheCommonsCodeDependency
    
    api jacksonCoreDependency
    api jacksonDatabindDependency
    
    runtimeOnly postgresqlDependency
    runtimeOnly sqliteJdbcDependency
    runtimeOnly derbyDependency
    
    testImplementation junitDependency
    testImplementation testContainersDependency
    testImplementation testContainersPostgresDependency
    
    //testImplementation s3MockDependency
    //testImplementation s3MockJunitDependency
    
    // Oracle JDBC would be a runtime dependency but since it's kinda hard to obtain the jar 
    // file, we just make it a integration test dependency. It will not appear in the pom.xml 
    // of the gretl plugin (jar).
    // We copy the jdbc jar into the docker image for our gretl-runtime. Login and password
    // is needed.
    // Those who want to run the integration tests need the jdbc jar too.
    integrationTestImplementation oracleDependency
    testImplementation testContainersOracleDependency    
    
    /**
     * Provides, at compile-time, the classes produced by the _main_ and _test_ SourceSets, 
     * allowing the integration tests to access the production code in _main_ and allowing 
     * them to reuse any unit test helper methods in _test_.
     */
    integrationTestImplementation sourceSets.main.output
    integrationTestImplementation sourceSets.test.output
    
    /**
     * Provides, at compile-time, the dependencies that both _main_ and _test_ require in order
     * to successfully compile.
     */
    integrationTestImplementation configurations.implementation
    integrationTestImplementation configurations.testImplementation
    
    /**
     * Provides, at run-time, the dependencies that both _main_ and _test_ require to run.
     */
    integrationTestImplementation configurations.runtime
    integrationTestImplementation configurations.testRuntime
}

gradlePlugin {
    plugins {
        gretlPlugin {
            id = 'ch.so.agi.gretl'
            implementationClass = 'ch.so.agi.gretl.gradle.GretlPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/sogis/gretl' 
    vcsUrl = 'https://github.com/sogis/gretl.git' 
    tags = ['etl', 'gis', 'sdi', 'geo', 'geoinformation', 'interlis', 'geopackage', 'shapefile', 'csv', 'database', 'postgresql', 'sqlite', 'ili2pg', 'ilivalidator'] 
    
    plugins { 
        gretlPlugin {  
            displayName = 'GRETL' 
            description = 'The Gradle gretl plugin extends gradle for use as a sql-centric (geo)data etl. gretl = gradle etl.' 
        }
    }
}

task setupPluginUpload() {
    description = "Converts Gradle plugin publish key and secret from environment variable to system property. (Workaround)"
    doLast {
	    def key=System.env.gradlePublishKey
	    def secret = System.env.gradlePublishSecret
	
	    if( !key || !secret)
	    {
	        throw new RuntimeException("gradlePublishKey and/or gradlePublishSecret are not defined environment variables")
	    }
	
	    System.properties.setProperty("gradle.publish.key", key)
	    System.properties.setProperty("gradle.publish.secret", secret)
    }
}
tasks.publishPlugins.dependsOn tasks.setupPluginUpload

test {
    useJUnit{
        // Exclude all Tests with dependency to a db server (pg, oracle, ..)
        excludeCategories 'ch.so.agi.gretl.testutil.DbTest'
        excludeCategories 'ch.so.agi.gretl.testutil.S3Test'
    }
}

task dbTest(type: Test, dependsOn: [compileJava, compileTestJava]) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs all unit tests with dependency to a db server (pg, oracle, ...).'
    useJUnit {
        includeCategories 'ch.so.agi.gretl.testutil.DbTest'
    }
}

def s3AccessKey = System.getProperty('s3AccessKey',properties.get('s3AccessKey'))
def s3SecretKey = System.getProperty('s3SecretKey',properties.get('s3SecretKey'))
def s3BucketName = System.getProperty('s3BucketName',properties.get('s3BucketName'))

task s3Test(type: Test, dependsOn: [compileJava, compileTestJava]) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs all unit tests with dependency to AWS S3.'
    
    useJUnit {
        includeCategories 'ch.so.agi.gretl.testutil.S3Test'
    }
}

// Transfer the required configuration variables to the test code.
// (The test code has no access to the -D or -P variables as it is executed in a separate jvm-process)
tasks.withType(Test) {
    def GRETLTEST_DBURI = 'gretltest_dburi_pg'
    systemProperty(GRETLTEST_DBURI, findProperty(GRETLTEST_DBURI))
    
    systemProperty('GRETL_PROJECT_ABS_PATH', project(':gretl').getProjectDir().getAbsolutePath())
    systemProperty('ROOT_PROJECT_ABS_PATH', project.getRootProject().getProjectDir().getAbsolutePath())
    
    systemProperty 's3AccessKey', s3AccessKey
    systemProperty 's3SecretKey', s3SecretKey
    systemProperty 's3BucketName', s3BucketName
}